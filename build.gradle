plugins {
  id 'java-library'
  id 'maven-publish'
}

group = 'me.hanju.webcollectorbase'
version = '0.3.0'
description = 'Base library for web collection projects with batch processing support'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
  withSourcesJar()
  withJavadocJar()
}

repositories {
  mavenCentral()
}

ext {
  lombokVersion = '1.18.38'
  slf4jVersion = '2.0.16'
  springBootVersion = '3.5.0'
  springVersion = '6.2.5'
  junitVersion = '5.11.4'
  junitPlatformVersion = '1.11.4'
}

dependencies {
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"

  compileOnly "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
  compileOnly "org.springframework:spring-context:${springVersion}"

  // Test
  testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
  testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'com.github.agent-hanju'
      artifactId = 'web-collector-base'
      version = project.version

      from components.java

      pom {
        name = 'Web Collector Base'
        description = project.description
        url = 'https://github.com/agent-hanju/web-collector-base'

        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id = 'agent-hanju'
            name = 'hanju'
            email = "agent.hanju@gmail.com"
          }
        }

        scm {
          connection = 'scm:git:git://github.com/agent-hanju/web-collector-base.git'
          developerConnection = 'scm:git:ssh://github.com:agent-hanju/web-collector-base.git'
          url = 'https://github.com/agent-hanju/web-collector-base'
        }
      }
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
  options.encoding = 'UTF-8'
  options.charSet = 'UTF-8'
  options.addStringOption('Xdoclint:none', '-quiet')
  failOnError = false
}

tasks.named('test') {
  useJUnitPlatform()
}
